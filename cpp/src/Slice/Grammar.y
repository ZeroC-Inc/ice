%code top{

//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

// Included first to get 'TokenContext' which we need to define YYLTYPE before flex does.
#include <Slice/GrammarUtil.h>

}

%code requires{

// Define a custom location type for storing the location (and filename) of tokens.
#define YYLTYPE Slice::TokenContext

// I must set the initial stack depth to the maximum stack depth to
// disable bison stack resizing. The bison stack resizing routines use
// simple malloc/alloc/memcpy calls, which do not work for the
// YYSTYPE, since YYSTYPE is a C++ type, with constructor, destructor,
// assignment operator, etc.
#define YYMAXDEPTH  10000
#define YYINITDEPTH YYMAXDEPTH

}

%code top{

// Defines the rule bison uses to reduce token locations. Bison asks that the macro should
// be one-line, and treatable as a single statement when followed by a semi-colon.
// `N` is the number of tokens that are being combined, and (Cur) is their combined location.
#define YYLLOC_DEFAULT(Cur, Rhs, N)                               \
do                                                                \
    if(N == 1)                                                    \
    {                                                             \
        (Cur) = (YYRHSLOC((Rhs), 1));                             \
    }                                                             \
    else                                                          \
    {                                                             \
        if(N)                                                     \
        {                                                         \
            (Cur).firstLine = (YYRHSLOC((Rhs), 1)).firstLine;     \
            (Cur).firstColumn = (YYRHSLOC((Rhs), 1)).firstColumn; \
        }                                                         \
        else                                                      \
        {                                                         \
            (Cur).firstLine = (YYRHSLOC((Rhs), 0)).lastLine;      \
            (Cur).firstColumn = (YYRHSLOC((Rhs), 0)).lastColumn;  \
        }                                                         \
        (Cur).filename = (YYRHSLOC((Rhs), N)).filename;           \
        (Cur).lastLine = (YYRHSLOC((Rhs), N)).lastLine;           \
        (Cur).lastColumn = (YYRHSLOC((Rhs), N)).lastColumn;       \
    }                                                             \
while(0)

}

%code{

// Forward declaration of the lexing function generated by flex, so bison knows about it.
// This must match the definition of 'yylex' (or 'slice_lex') in the generated scanner.
int slice_lex(YYSTYPE* lvalp, YYLTYPE* llocp);

}

%{

#include <IceUtil/InputUtil.h>
#include <IceUtil/UUID.h>
#include <cstring>

#ifdef _MSC_VER
// warning C4102: 'yyoverflowlab' : unreferenced label
#   pragma warning(disable:4102)
// warning C4065: switch statement contains 'default' but no 'case' labels
#   pragma warning(disable:4065)
// warning C4244: '=': conversion from 'int' to 'yytype_int16', possible loss of data
#   pragma warning(disable:4244)
// warning C4127: conditional expression is constant
#   pragma warning(disable:4127)
#endif

// Avoid old style cast warnings in generated grammar
#ifdef __GNUC__
#  pragma GCC diagnostic ignored "-Wold-style-cast"
#  pragma GCC diagnostic ignored "-Wunused-label"

// See https://gcc.gnu.org/bugzilla/show_bug.cgi?id=98753
#  pragma GCC diagnostic ignored "-Wfree-nonheap-object"
#endif

// Avoid clang warnings in generate grammar
#if defined(__clang__)
#   pragma clang diagnostic ignored "-Wconversion"
#   pragma clang diagnostic ignored "-Wsign-conversion"
#   pragma clang diagnostic ignored "-Wunused-but-set-variable"
#endif

using namespace std;
using namespace Slice;

void
slice_error(const char* s)
{
    // yacc and recent versions of Bison use "syntax error" instead
    // of "parse error".

    if (strcmp(s, "parse error") == 0)
    {
        unit->error("syntax error");
    }
    else
    {
        unit->error(s);
    }
}

%}

// Directs Bison to generate a re-entrant parser.
%define api.pure
// Specifies what type to back the tokens with (their semantic values).
%define api.value.type {Slice::GrammarBasePtr}
// Enables Bison's token location tracking functionality.
%locations

// All keyword tokens. Make sure to modify the "keyword" rule in this
// file if the list of keywords is changed. Also make sure to add the
// keyword to the keyword table in Scanner.l.
%token ICE_MODULE
%token ICE_CLASS
%token ICE_INTERFACE
%token ICE_EXCEPTION
%token ICE_STRUCT
%token ICE_SEQUENCE
%token ICE_DICTIONARY
%token ICE_ENUM
%token ICE_OUT
%token ICE_EXTENDS
%token ICE_THROWS
%token ICE_VOID
%token ICE_BOOL
%token ICE_BYTE
%token ICE_SHORT
%token ICE_INT
%token ICE_LONG
%token ICE_FLOAT
%token ICE_DOUBLE
%token ICE_STRING
%token ICE_OBJECT
%token ICE_CONST
%token ICE_FALSE
%token ICE_TRUE
%token ICE_IDEMPOTENT
%token ICE_TAG
%token ICE_OPTIONAL
%token ICE_VALUE

// Other tokens.
%token ICE_STRING_LITERAL
%token ICE_INTEGER_LITERAL
%token ICE_FLOATING_POINT_LITERAL
%token ICE_IDENTIFIER
%token ICE_SCOPED_IDENTIFIER
%token ICE_METADATA_OPEN
%token ICE_METADATA_CLOSE
%token ICE_GLOBAL_METADATA_OPEN
%token ICE_GLOBAL_METADATA_IGNORE
%token ICE_GLOBAL_METADATA_CLOSE

// Here 'OPEN' means these tokens end with an open parenthesis.
%token ICE_IDENT_OPEN
%token ICE_KEYWORD_OPEN
%token ICE_TAG_OPEN
%token ICE_OPTIONAL_OPEN

%token BAD_CHAR

%%

// ----------------------------------------------------------------------
start
// ----------------------------------------------------------------------
: definitions
{
}
;

// ----------------------------------------------------------------------
opt_semicolon
// ----------------------------------------------------------------------
: ';'
{
}
| %empty
{
}
;

// ----------------------------------------------------------------------
global_meta_data
// ----------------------------------------------------------------------
: ICE_GLOBAL_METADATA_OPEN string_list ICE_GLOBAL_METADATA_CLOSE
{
    $$ = $2;
}
| ICE_GLOBAL_METADATA_IGNORE string_list ICE_GLOBAL_METADATA_CLOSE
{
    unit->error("global metadata must appear before any definitions");
    $$ = $2; // Dummy
}
;

// ----------------------------------------------------------------------
meta_data
// ----------------------------------------------------------------------
: ICE_METADATA_OPEN string_list ICE_METADATA_CLOSE
{
    $$ = $2;
}
| %empty
{
    $$ = make_shared<StringListTok>();
}
;

// ----------------------------------------------------------------------
definitions
// ----------------------------------------------------------------------
: definitions global_meta_data
{
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($2);
    if(!metaData->v.empty())
    {
        unit->addGlobalMetaData(metaData->v);
    }
}
| definitions meta_data definition
{
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($2);
    ContainedPtr contained = dynamic_pointer_cast<Contained>($3);
    if(contained && !metaData->v.empty())
    {
        contained->setMetaData(metaData->v);
    }
}
| %empty
{
}
;

// ----------------------------------------------------------------------
definition
// ----------------------------------------------------------------------
: module_def
{
    assert($1 == 0 || dynamic_pointer_cast<Module>($1));
}
opt_semicolon
| class_decl
{
    assert($1 == 0 || dynamic_pointer_cast<ClassDecl>($1));
}
';'
| class_decl
{
    unit->error("`;' missing after class forward declaration");
}
| class_def
{
    assert($1 == 0 || dynamic_pointer_cast<ClassDef>($1));
}
opt_semicolon
| interface_decl
{
    assert($1 == 0 || dynamic_pointer_cast<InterfaceDecl>($1));
}
';'
| interface_decl
{
    unit->error("`;' missing after interface forward declaration");
}
| interface_def
{
    assert($1 == 0 || dynamic_pointer_cast<InterfaceDef>($1));
}
opt_semicolon
| exception_decl
{
    assert($1 == 0);
}
';'
| exception_decl
{
    unit->error("`;' missing after exception forward declaration");
}
| exception_def
{
    assert($1 == 0 || dynamic_pointer_cast<Exception>($1));
}
opt_semicolon
| struct_decl
{
    assert($1 == 0);
}
';'
| struct_decl
{
    unit->error("`;' missing after struct forward declaration");
}
| struct_def
{
    assert($1 == 0 || dynamic_pointer_cast<Struct>($1));
}
opt_semicolon
| sequence_def
{
    assert($1 == 0 || dynamic_pointer_cast<Sequence>($1));
}
';'
| sequence_def
{
    unit->error("`;' missing after sequence definition");
}
| dictionary_def
{
    assert($1 == 0 || dynamic_pointer_cast<Dictionary>($1));
}
';'
| dictionary_def
{
    unit->error("`;' missing after dictionary definition");
}
| enum_def
{
    assert($1 == 0 || dynamic_pointer_cast<Enum>($1));
}
opt_semicolon
| const_def
{
    assert($1 == 0 || dynamic_pointer_cast<Const>($1));
}
';'
| const_def
{
    unit->error("`;' missing after const definition");
}
| error ';'
{
    yyerrok;
}
;

// ----------------------------------------------------------------------
module_def
// ----------------------------------------------------------------------
: ICE_MODULE ICE_IDENTIFIER
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    ContainerPtr cont = unit->currentContainer();
    ModulePtr module = cont->createModule(ident->v);
    if(module)
    {
        cont->checkIntroduced(ident->v, module);
        unit->pushContainer(module);
        $$ = module;
    }
    else
    {
        $$ = 0;
    }
}
'{' definitions '}'
{
    if($3)
    {
        unit->popContainer();
        $$ = $3;
    }
    else
    {
        $$ = 0;
    }
}
;

// ----------------------------------------------------------------------
exception_id
// ----------------------------------------------------------------------
: ICE_EXCEPTION ICE_IDENTIFIER
{
    $$ = $2;
}
| ICE_EXCEPTION keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    unit->error("keyword `" + ident->v + "' cannot be used as exception name");
    $$ = $2; // Dummy
}
;

// ----------------------------------------------------------------------
exception_decl
// ----------------------------------------------------------------------
: exception_id
{
    unit->error("exceptions cannot be forward declared");
    $$ = 0;
}
;

// ----------------------------------------------------------------------
exception_def
// ----------------------------------------------------------------------
: exception_id exception_extends
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    ExceptionPtr base = dynamic_pointer_cast<Exception>($2);
    ContainerPtr cont = unit->currentContainer();
    ExceptionPtr ex = cont->createException(ident->v, base);
    if(ex)
    {
        cont->checkIntroduced(ident->v, ex);
        unit->pushContainer(ex);
    }
    $$ = ex;
}
'{' data_members '}'
{
    if($3)
    {
        unit->popContainer();
    }
    $$ = $3;
}
;

// ----------------------------------------------------------------------
exception_extends
// ----------------------------------------------------------------------
: extends scoped_name
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($2);
    ContainerPtr cont = unit->currentContainer();
    ContainedPtr contained = cont->lookupException(scoped->v);
    cont->checkIntroduced(scoped->v);
    $$ = contained;
}
| %empty
{
    $$ = 0;
}
;

// ----------------------------------------------------------------------
type_id
// ----------------------------------------------------------------------
: type ICE_IDENTIFIER
{
    TypePtr type = dynamic_pointer_cast<Type>($1);
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    TypeStringTokPtr typestring = make_shared<TypeStringTok>();
    typestring->v = make_pair(type, ident->v);
    $$ = typestring;
}
;

// ----------------------------------------------------------------------
tag
// ----------------------------------------------------------------------
: ICE_TAG_OPEN ICE_INTEGER_LITERAL ')'
{
    IntegerTokPtr i = dynamic_pointer_cast<IntegerTok>($2);

    int tag;
    if(i->v < 0 || i->v > Int32Max)
    {
        unit->error("tag is out of range");
        tag = -1;
    }
    else
    {
        tag = static_cast<int>(i->v);
    }

    TaggedDefTokPtr m = make_shared<TaggedDefTok>(tag);
    $$ = m;
}
| ICE_TAG_OPEN scoped_name ')'
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($2);

    ContainerPtr cont = unit->currentContainer();
    assert(cont);
    ContainedList cl = cont->lookupContained(scoped->v, false);
    if(cl.empty())
    {
        EnumeratorList enumerators = cont->enumerators(scoped->v);
        if(enumerators.size() == 1)
        {
            // Found
            cl.push_back(enumerators.front());
            scoped->v = enumerators.front()->scoped();
            unit->warning(Deprecated, string("referencing enumerator `") + scoped->v
                          + "' without its enumeration's scope is deprecated");
        }
        else if(enumerators.size() > 1)
        {
            ostringstream os;
            os << "enumerator `" << scoped->v << "' could designate";
            bool first = true;
            for(EnumeratorList::iterator p = enumerators.begin(); p != enumerators.end(); ++p)
            {
                if(first)
                {
                    first = false;
                }
                else
                {
                    os << " or";
                }

                os << " `" << (*p)->scoped() << "'";
            }
            unit->error(os.str());
        }
        else
        {
            unit->error(string("`") + scoped->v + "' is not defined");
        }
    }

    if(cl.empty())
    {
        YYERROR; // Can't continue, jump to next yyerrok
    }
    cont->checkIntroduced(scoped->v);

    int tag = -1;
    EnumeratorPtr enumerator = dynamic_pointer_cast<Enumerator>(cl.front());
    ConstPtr constant = dynamic_pointer_cast<Const>(cl.front());
    if(constant)
    {
        BuiltinPtr b = dynamic_pointer_cast<Builtin>(constant->type());
        if(b && b->isIntegralType())
        {
            IceUtil::Int64 l = IceUtilInternal::strToInt64(constant->value().c_str(), 0, 0);
            if(l < 0 || l > Int32Max)
            {
                unit->error("tag is out of range");
            }
            tag = static_cast<int>(l);
        }
    }
    else if(enumerator)
    {
        tag = enumerator->value();
    }

    if(tag < 0)
    {
        unit->error("invalid tag `" + scoped->v + "'");
    }

    TaggedDefTokPtr m = make_shared<TaggedDefTok>(tag);
    $$ = m;
}
| ICE_TAG_OPEN ')'
{
    unit->error("missing tag");
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1); // Dummy
    $$ = m;
}
| ICE_TAG
{
    unit->error("missing tag");
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1); // Dummy
    $$ = m;
}
;

// ----------------------------------------------------------------------
optional
// ----------------------------------------------------------------------
: ICE_OPTIONAL_OPEN ICE_INTEGER_LITERAL ')'
{
    IntegerTokPtr i = dynamic_pointer_cast<IntegerTok>($2);

    int tag;
    if(i->v < 0 || i->v > Int32Max)
    {
        unit->error("tag is out of range");
        tag = -1;
    }
    else
    {
        tag = static_cast<int>(i->v);
    }

    TaggedDefTokPtr m = make_shared<TaggedDefTok>(tag);
    $$ = m;
}
| ICE_OPTIONAL_OPEN scoped_name ')'
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($2);
    ContainerPtr cont = unit->currentContainer();
    assert(cont);
    ContainedList cl = cont->lookupContained(scoped->v, false);
    if(cl.empty())
    {
        EnumeratorList enumerators = cont->enumerators(scoped->v);
        if(enumerators.size() == 1)
        {
            // Found
            cl.push_back(enumerators.front());
            scoped->v = enumerators.front()->scoped();
            unit->warning(Deprecated, string("referencing enumerator `") + scoped->v
                          + "' without its enumeration's scope is deprecated");
        }
        else if(enumerators.size() > 1)
        {
            ostringstream os;
            os << "enumerator `" << scoped->v << "' could designate";
            bool first = true;
            for(EnumeratorList::iterator p = enumerators.begin(); p != enumerators.end(); ++p)
            {
                if(first)
                {
                    first = false;
                }
                else
                {
                    os << " or";
                }

                os << " `" << (*p)->scoped() << "'";
            }
            unit->error(os.str());
        }
        else
        {
            unit->error(string("`") + scoped->v + "' is not defined");
        }
    }

    if(cl.empty())
    {
        YYERROR; // Can't continue, jump to next yyerrok
    }
    cont->checkIntroduced(scoped->v);

    int tag = -1;
    EnumeratorPtr enumerator = dynamic_pointer_cast<Enumerator>(cl.front());
    ConstPtr constant = dynamic_pointer_cast<Const>(cl.front());
    if(constant)
    {
        BuiltinPtr b = dynamic_pointer_cast<Builtin>(constant->type());
        if(b && b->isIntegralType())
        {
            IceUtil::Int64 l = IceUtilInternal::strToInt64(constant->value().c_str(), 0, 0);
            if(l < 0 || l > Int32Max)
            {
                unit->error("tag is out of range");
            }
            tag = static_cast<int>(l);
        }
    }
    else if(enumerator)
    {
        tag = enumerator->value();
    }

    if(tag < 0)
    {
        unit->error("invalid tag `" + scoped->v + "'");
    }

    TaggedDefTokPtr m = make_shared<TaggedDefTok>(tag);
    $$ = m;
}
| ICE_OPTIONAL_OPEN ')'
{
    unit->error("missing tag");
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1); // Dummy
    $$ = m;
}
| ICE_OPTIONAL
{
    unit->error("missing tag");
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1); // Dummy
    $$ = m;
}
;

// ----------------------------------------------------------------------
tagged_type_id
// ----------------------------------------------------------------------
: tag type_id
{
    TaggedDefTokPtr m = dynamic_pointer_cast<TaggedDefTok>($1);
    TypeStringTokPtr ts = dynamic_pointer_cast<TypeStringTok>($2);

    m->type = ts->v.first;
    m->name = ts->v.second;
    $$ = m;
}
| optional type_id
{
    TaggedDefTokPtr m = dynamic_pointer_cast<TaggedDefTok>($1);
    TypeStringTokPtr ts = dynamic_pointer_cast<TypeStringTok>($2);

    m->type = ts->v.first;
    m->name = ts->v.second;
    $$ = m;
}
| type_id
{
    TypeStringTokPtr ts = dynamic_pointer_cast<TypeStringTok>($1);
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1);
    m->type = ts->v.first;
    m->name = ts->v.second;
    $$ = m;
}
;

// ----------------------------------------------------------------------
struct_id
// ----------------------------------------------------------------------
: ICE_STRUCT ICE_IDENTIFIER
{
    $$ = $2;
}
| ICE_STRUCT keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    unit->error("keyword `" + ident->v + "' cannot be used as struct name");
    $$ = $2; // Dummy
}
;

// ----------------------------------------------------------------------
struct_decl
// ----------------------------------------------------------------------
: struct_id
{
    unit->error("structs cannot be forward declared");
    $$ = 0; // Dummy
}
;

// ----------------------------------------------------------------------
struct_def
// ----------------------------------------------------------------------
: struct_id
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    StructPtr st = cont->createStruct(ident->v);
    if(st)
    {
        cont->checkIntroduced(ident->v, st);
        unit->pushContainer(st);
    }
    else
    {
        st = cont->createStruct(IceUtil::generateUUID()); // Dummy
        assert(st);
        unit->pushContainer(st);
    }
    $$ = st;
}
'{' data_members '}'
{
    if($2)
    {
        unit->popContainer();
    }
    $$ = $2;

    //
    // Empty structures are not allowed
    //
    StructPtr st = dynamic_pointer_cast<Struct>($$);
    assert(st);
    if(st->dataMembers().empty())
    {
        unit->error("struct `" + st->name() + "' must have at least one member"); // $$ is a dummy
    }
}
;

// ----------------------------------------------------------------------
class_name
// ----------------------------------------------------------------------
: ICE_CLASS ICE_IDENTIFIER
{
    $$ = $2;
}
| ICE_CLASS keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    unit->error("keyword `" + ident->v + "' cannot be used as class name");
    $$ = $2; // Dummy
}
;

// ----------------------------------------------------------------------
class_id
// ----------------------------------------------------------------------
: ICE_CLASS ICE_IDENT_OPEN ICE_INTEGER_LITERAL ')'
{
    IceUtil::Int64 id = dynamic_pointer_cast<IntegerTok>($3)->v;
    if(id < 0)
    {
        unit->error("invalid compact id for class: id must be a positive integer");
    }
    else if(id > Int32Max)
    {
        unit->error("invalid compact id for class: value is out of range");
    }
    else
    {
        string typeId = unit->getTypeId(static_cast<int>(id));
        if(!typeId.empty() && !unit->ignRedefs())
        {
            unit->error("invalid compact id for class: already assigned to class `" + typeId + "'");
        }
    }

    ClassIdTokPtr classId = make_shared<ClassIdTok>();
    classId->v = dynamic_pointer_cast<StringTok>($2)->v;
    classId->t = static_cast<int>(id);
    $$ = classId;
}
| ICE_CLASS ICE_IDENT_OPEN scoped_name ')'
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($3);

    ContainerPtr cont = unit->currentContainer();
    assert(cont);
    ContainedList cl = cont->lookupContained(scoped->v, false);
    if(cl.empty())
    {
        EnumeratorList enumerators = cont->enumerators(scoped->v);
        if(enumerators.size() == 1)
        {
            // Found
            cl.push_back(enumerators.front());
            scoped->v = enumerators.front()->scoped();
            unit->warning(Deprecated, string("referencing enumerator `") + scoped->v
                          + "' without its enumeration's scope is deprecated");
        }
        else if(enumerators.size() > 1)
        {
            ostringstream os;
            os << "enumerator `" << scoped->v << "' could designate";
            bool first = true;
            for(EnumeratorList::iterator p = enumerators.begin(); p != enumerators.end(); ++p)
            {
                if(first)
                {
                    first = false;
                }
                else
                {
                    os << " or";
                }

                os << " `" << (*p)->scoped() << "'";
            }
            unit->error(os.str());
        }
        else
        {
            unit->error(string("`") + scoped->v + "' is not defined");
        }
    }

    if(cl.empty())
    {
        YYERROR; // Can't continue, jump to next yyerrok
    }
    cont->checkIntroduced(scoped->v);

    int id = -1;
    EnumeratorPtr enumerator = dynamic_pointer_cast<Enumerator>(cl.front());
    ConstPtr constant = dynamic_pointer_cast<Const>(cl.front());
    if(constant)
    {
        BuiltinPtr b = dynamic_pointer_cast<Builtin>(constant->type());
        if(b && b->isIntegralType())
        {
            IceUtil::Int64 l = IceUtilInternal::strToInt64(constant->value().c_str(), 0, 0);
            if(l < 0 || l > Int32Max)
            {
                unit->error("compact id for class is out of range");
            }
            id = static_cast<int>(l);
        }
    }
    else if(enumerator)
    {
        id = enumerator->value();
    }

    if(id < 0)
    {
        unit->error("invalid compact id for class: id must be a positive integer");
    }
    else
    {
        string typeId = unit->getTypeId(id);
        if(!typeId.empty() && !unit->ignRedefs())
        {
            unit->error("invalid compact id for class: already assigned to class `" + typeId + "'");
        }
    }

    ClassIdTokPtr classId = make_shared<ClassIdTok>();
    classId->v = dynamic_pointer_cast<StringTok>($2)->v;
    classId->t = id;
    $$ = classId;

}
| class_name
{
    ClassIdTokPtr classId = make_shared<ClassIdTok>();
    classId->v = dynamic_pointer_cast<StringTok>($1)->v;
    classId->t = -1;
    $$ = classId;
}
;

// ----------------------------------------------------------------------
class_decl
// ----------------------------------------------------------------------
: class_name
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    ClassDeclPtr cl = cont->createClassDecl(ident->v);
    $$ = cl;
}
;

// ----------------------------------------------------------------------
class_def
// ----------------------------------------------------------------------
: class_id class_extends
{
    ClassIdTokPtr ident = dynamic_pointer_cast<ClassIdTok>($1);
    ContainerPtr cont = unit->currentContainer();
    ClassDefPtr base = dynamic_pointer_cast<ClassDef>($2);
    ClassDefPtr cl = cont->createClassDef(ident->v, ident->t, base);
    if(cl)
    {
        cont->checkIntroduced(ident->v, cl);
        unit->pushContainer(cl);
        $$ = cl;
    }
    else
    {
        $$ = 0;
    }
}
'{' data_members '}'
{
    if($3)
    {
        unit->popContainer();
        $$ = $3;
    }
    else
    {
        $$ = 0;
    }
}
;

// ----------------------------------------------------------------------
class_extends
// ----------------------------------------------------------------------
: extends scoped_name
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($2);
    ContainerPtr cont = unit->currentContainer();
    TypeList types = cont->lookupType(scoped->v);
    $$ = 0;
    if(!types.empty())
    {
        ClassDeclPtr cl = dynamic_pointer_cast<ClassDecl>(types.front());
        if(!cl)
        {
            string msg = "`";
            msg += scoped->v;
            msg += "' is not a class";
            unit->error(msg);
        }
        else
        {
            ClassDefPtr def = cl->definition();
            if(!def)
            {
                string msg = "`";
                msg += scoped->v;
                msg += "' has been declared but not defined";
                unit->error(msg);
            }
            else
            {
                cont->checkIntroduced(scoped->v);
                $$ = def;
            }
        }
    }
}
| %empty
{
    $$ = 0;
}
;

// ----------------------------------------------------------------------
extends
// ----------------------------------------------------------------------
: ICE_EXTENDS
{
}
| ':'
{
}
;

// ----------------------------------------------------------------------
data_members
// ----------------------------------------------------------------------
: meta_data data_member ';' data_members
{
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($1);
    ContainedPtr contained = dynamic_pointer_cast<Contained>($2);
    if(contained && !metaData->v.empty())
    {
        contained->setMetaData(metaData->v);
    }
}
| error ';' data_members
{
}
| meta_data data_member
{
    unit->error("`;' missing after definition");
}
| %empty
{
}
;

// ----------------------------------------------------------------------
data_member
// ----------------------------------------------------------------------
: tagged_type_id
{
    TaggedDefTokPtr def = dynamic_pointer_cast<TaggedDefTok>($1);
    ClassDefPtr cl = dynamic_pointer_cast<ClassDef>(unit->currentContainer());
    DataMemberPtr dm;
    if(cl)
    {
        dm = cl->createDataMember(def->name, def->type, def->isTagged, def->tag, 0, "", "");
    }
    StructPtr st = dynamic_pointer_cast<Struct>(unit->currentContainer());
    if(st)
    {
        if (def->isTagged)
        {
            unit->error("tagged data members are not supported in structs");
            dm = st->createDataMember(def->name, def->type, false, 0, 0, "", ""); // Dummy
        }
        else
        {
            dm = st->createDataMember(def->name, def->type, false, -1, 0, "", "");
        }
    }
    ExceptionPtr ex = dynamic_pointer_cast<Exception>(unit->currentContainer());
    if(ex)
    {
        dm = ex->createDataMember(def->name, def->type, def->isTagged, def->tag, 0, "", "");
    }
    unit->currentContainer()->checkIntroduced(def->name, dm);
    $$ = dm;
}
| tagged_type_id '=' const_initializer
{
    TaggedDefTokPtr def = dynamic_pointer_cast<TaggedDefTok>($1);
    ConstDefTokPtr value = dynamic_pointer_cast<ConstDefTok>($3);

    ClassDefPtr cl = dynamic_pointer_cast<ClassDef>(unit->currentContainer());
    DataMemberPtr dm;
    if(cl)
    {
        dm = cl->createDataMember(def->name, def->type, def->isTagged, def->tag, value->v,
                                  value->valueAsString, value->valueAsLiteral);
    }
    StructPtr st = dynamic_pointer_cast<Struct>(unit->currentContainer());
    if(st)
    {
        if (def->isTagged)
        {
            unit->error("tagged data members are not supported in structs");
            dm = st->createDataMember(def->name, def->type, false, 0, 0, "", ""); // Dummy
        }
        else
        {
            dm = st->createDataMember(def->name, def->type, false, -1, value->v,
                                      value->valueAsString, value->valueAsLiteral);
        }
    }
    ExceptionPtr ex = dynamic_pointer_cast<Exception>(unit->currentContainer());
    if(ex)
    {
        dm = ex->createDataMember(def->name, def->type, def->isTagged, def->tag, value->v,
                                  value->valueAsString, value->valueAsLiteral);
    }
    unit->currentContainer()->checkIntroduced(def->name, dm);
    $$ = dm;
}
| type keyword
{
    TypePtr type = dynamic_pointer_cast<Type>($1);
    string name = dynamic_pointer_cast<StringTok>($2)->v;
    ClassDefPtr cl = dynamic_pointer_cast<ClassDef>(unit->currentContainer());
    if(cl)
    {
        $$ = cl->createDataMember(name, type, false, 0, 0, "", ""); // Dummy
    }
    StructPtr st = dynamic_pointer_cast<Struct>(unit->currentContainer());
    if(st)
    {
        $$ = st->createDataMember(name, type, false, 0, 0, "", ""); // Dummy
    }
    ExceptionPtr ex = dynamic_pointer_cast<Exception>(unit->currentContainer());
    if(ex)
    {
        $$ = ex->createDataMember(name, type, false, 0, 0, "", ""); // Dummy
    }
    assert($$);
    unit->error("keyword `" + name + "' cannot be used as data member name");
}
| type
{
    TypePtr type = dynamic_pointer_cast<Type>($1);
    ClassDefPtr cl = dynamic_pointer_cast<ClassDef>(unit->currentContainer());
    if(cl)
    {
        $$ = cl->createDataMember(IceUtil::generateUUID(), type, false, 0, 0, "", ""); // Dummy
    }
    StructPtr st = dynamic_pointer_cast<Struct>(unit->currentContainer());
    if(st)
    {
        $$ = st->createDataMember(IceUtil::generateUUID(), type, false, 0, 0, "", ""); // Dummy
    }
    ExceptionPtr ex = dynamic_pointer_cast<Exception>(unit->currentContainer());
    if(ex)
    {
        $$ = ex->createDataMember(IceUtil::generateUUID(), type, false, 0, 0, "", ""); // Dummy
    }
    assert($$);
    unit->error("missing data member name");
}
;

// ----------------------------------------------------------------------
return_type
// ----------------------------------------------------------------------
: tag type
{
    TaggedDefTokPtr m = dynamic_pointer_cast<TaggedDefTok>($1);
    m->type = dynamic_pointer_cast<Type>($2);
    $$ = m;
}
| optional type
{
    TaggedDefTokPtr m = dynamic_pointer_cast<TaggedDefTok>($1);
    m->type = dynamic_pointer_cast<Type>($2);
    $$ = m;
}
| type
{
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1);
    m->type = dynamic_pointer_cast<Type>($1);
    $$ = m;
}
| ICE_VOID
{
    TaggedDefTokPtr m = make_shared<TaggedDefTok>(-1);
    $$ = m;
}
;

// ----------------------------------------------------------------------
operation_preamble
// ----------------------------------------------------------------------
: return_type ICE_IDENT_OPEN
{
    TaggedDefTokPtr returnType = dynamic_pointer_cast<TaggedDefTok>($1);
    string name = dynamic_pointer_cast<StringTok>($2)->v;
    InterfaceDefPtr interface = dynamic_pointer_cast<InterfaceDef>(unit->currentContainer());
    if(interface)
    {
        OperationPtr op = interface->createOperation(name, returnType->type, returnType->isTagged, returnType->tag);
        if(op)
        {
            interface->checkIntroduced(name, op);
            unit->pushContainer(op);
            $$ = op;
        }
        else
        {
            $$ = 0;
        }
    }
    else
    {
        $$ = 0;
    }
}
| ICE_IDEMPOTENT return_type ICE_IDENT_OPEN
{
    TaggedDefTokPtr returnType = dynamic_pointer_cast<TaggedDefTok>($2);
    string name = dynamic_pointer_cast<StringTok>($3)->v;
    InterfaceDefPtr interface = dynamic_pointer_cast<InterfaceDef>(unit->currentContainer());
    if(interface)
    {
        OperationPtr op = interface->createOperation(name, returnType->type, returnType->isTagged, returnType->tag,
                                                Operation::Idempotent);
        if(op)
        {
            interface->checkIntroduced(name, op);
            unit->pushContainer(op);
            $$ = op;
        }
        else
        {
            $$ = 0;
        }
    }
    else
    {
        $$ = 0;
    }
}
| return_type ICE_KEYWORD_OPEN
{
    TaggedDefTokPtr returnType = dynamic_pointer_cast<TaggedDefTok>($1);
    string name = dynamic_pointer_cast<StringTok>($2)->v;
    InterfaceDefPtr interface = dynamic_pointer_cast<InterfaceDef>(unit->currentContainer());
    if(interface)
    {
        OperationPtr op = interface->createOperation(name, returnType->type, returnType->isTagged, returnType->tag);
        if(op)
        {
            unit->pushContainer(op);
            unit->error("keyword `" + name + "' cannot be used as operation name");
            $$ = op; // Dummy
        }
        else
        {
            $$ = 0;
        }
    }
    else
    {
        $$ = 0;
    }
}
| ICE_IDEMPOTENT return_type ICE_KEYWORD_OPEN
{
    TaggedDefTokPtr returnType = dynamic_pointer_cast<TaggedDefTok>($2);
    string name = dynamic_pointer_cast<StringTok>($3)->v;
    InterfaceDefPtr interface = dynamic_pointer_cast<InterfaceDef>(unit->currentContainer());
    if(interface)
    {
        OperationPtr op = interface->createOperation(name, returnType->type, returnType->isTagged, returnType->tag,
                                                Operation::Idempotent);
        if(op)
        {
            unit->pushContainer(op);
            unit->error("keyword `" + name + "' cannot be used as operation name");
            $$ = op; // Dummy
        }
        else
        {
            return 0;
        }
    }
    else
    {
        $$ = 0;
    }
}
;

// ----------------------------------------------------------------------
operation
// ----------------------------------------------------------------------
: operation_preamble parameters ')'
{
    if($1)
    {
        unit->popContainer();
        $$ = $1;
    }
    else
    {
        $$ = 0;
    }
}
throws
{
    OperationPtr op = dynamic_pointer_cast<Operation>($4);
    ExceptionListTokPtr el = dynamic_pointer_cast<ExceptionListTok>($5);
    assert(el);
    if(op)
    {
        op->setExceptionList(el->v);
    }
}
| operation_preamble error ')'
{
    if($1)
    {
        unit->popContainer();
    }
    yyerrok;
}
throws
{
    OperationPtr op = dynamic_pointer_cast<Operation>($4);
    ExceptionListTokPtr el = dynamic_pointer_cast<ExceptionListTok>($5);
    assert(el);
    if(op)
    {
        op->setExceptionList(el->v); // Dummy
    }
}
;

// ----------------------------------------------------------------------
interface_id
// ----------------------------------------------------------------------
: ICE_INTERFACE ICE_IDENTIFIER
{
    $$ = $2;
}
| ICE_INTERFACE keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    unit->error("keyword `" + ident->v + "' cannot be used as interface name");
    $$ = $2; // Dummy
}
;

// ----------------------------------------------------------------------
interface_decl
// ----------------------------------------------------------------------
: interface_id
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    InterfaceDeclPtr cl = cont->createInterfaceDecl(ident->v);
    cont->checkIntroduced(ident->v, cl);
    $$ = cl;
}
;

// ----------------------------------------------------------------------
interface_def
// ----------------------------------------------------------------------
: interface_id interface_extends
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    InterfaceListTokPtr bases = dynamic_pointer_cast<InterfaceListTok>($2);
    InterfaceDefPtr interface = cont->createInterfaceDef(ident->v, bases->v);
    if(interface)
    {
        cont->checkIntroduced(ident->v, interface);
        unit->pushContainer(interface);
        $$ = interface;
    }
    else
    {
        $$ = 0;
    }
}
'{' operations '}'
{
    if($3)
    {
        unit->popContainer();
        $$ = $3;
    }
    else
    {
        $$ = 0;
    }
}
;

// ----------------------------------------------------------------------
interface_list
// ----------------------------------------------------------------------
: scoped_name ',' interface_list
{
    InterfaceListTokPtr intfs = dynamic_pointer_cast<InterfaceListTok>($3);
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    TypeList types = cont->lookupType(scoped->v);
    if(!types.empty())
    {
        InterfaceDeclPtr interface = dynamic_pointer_cast<InterfaceDecl>(types.front());
        if(!interface)
        {
            string msg = "`";
            msg += scoped->v;
            msg += "' is not an interface";
            unit->error(msg);
        }
        else
        {
            InterfaceDefPtr def = interface->definition();
            if(!def)
            {
                string msg = "`";
                msg += scoped->v;
                msg += "' has been declared but not defined";
                unit->error(msg);
            }
            else
            {
                cont->checkIntroduced(scoped->v);
                intfs->v.push_front(def);
            }
        }
    }
    $$ = intfs;
}
| scoped_name
{
    InterfaceListTokPtr intfs = make_shared<InterfaceListTok>();
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    TypeList types = cont->lookupType(scoped->v);
    if(!types.empty())
    {
        InterfaceDeclPtr interface = dynamic_pointer_cast<InterfaceDecl>(types.front());
        if(!interface)
        {
            string msg = "`";
            msg += scoped->v;
            msg += "' is not an interface";
            unit->error(msg); // $$ is a dummy
        }
        else
        {
            InterfaceDefPtr def = interface->definition();
            if(!def)
            {
                string msg = "`";
                msg += scoped->v;
                msg += "' has been declared but not defined";
                unit->error(msg); // $$ is a dummy
            }
            else
            {
                cont->checkIntroduced(scoped->v);
                intfs->v.push_front(def);
            }
        }
    }
    $$ = intfs;
}
| ICE_OBJECT
{
    unit->error("illegal inheritance from type Object");
    $$ = make_shared<InterfaceListTok>(); // Dummy
}
| ICE_VALUE
{
    unit->error("illegal inheritance from type Value");
    $$ = make_shared<ClassListTok>(); // Dummy
}
;

// ----------------------------------------------------------------------
interface_extends
// ----------------------------------------------------------------------
: extends interface_list
{
    $$ = $2;
}
| %empty
{
    $$ = make_shared<InterfaceListTok>();
}
;

// ----------------------------------------------------------------------
operations
// ----------------------------------------------------------------------
: meta_data operation ';' operations
{
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($1);
    ContainedPtr contained = dynamic_pointer_cast<Contained>($2);
    if(contained && !metaData->v.empty())
    {
        contained->setMetaData(metaData->v);
    }
}
| error ';' operations
{
}
| meta_data operation
{
    unit->error("`;' missing after definition");
}
| %empty
{
}
;

// ----------------------------------------------------------------------
exception_list
// ----------------------------------------------------------------------
: exception ',' exception_list
{
    ExceptionPtr exception = dynamic_pointer_cast<Exception>($1);
    ExceptionListTokPtr exceptionList = dynamic_pointer_cast<ExceptionListTok>($3);
    exceptionList->v.push_front(exception);
    $$ = exceptionList;
}
| exception
{
    ExceptionPtr exception = dynamic_pointer_cast<Exception>($1);
    ExceptionListTokPtr exceptionList = make_shared<ExceptionListTok>();
    exceptionList->v.push_front(exception);
    $$ = exceptionList;
}
;

// ----------------------------------------------------------------------
exception
// ----------------------------------------------------------------------
: scoped_name
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    ExceptionPtr exception = cont->lookupException(scoped->v);
    if(!exception)
    {
        exception = cont->createException(IceUtil::generateUUID(), 0, Dummy); // Dummy
    }
    cont->checkIntroduced(scoped->v, exception);
    $$ = exception;
}
| keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    unit->error("keyword `" + ident->v + "' cannot be used as exception name");
    $$ = unit->currentContainer()->createException(IceUtil::generateUUID(), 0, Dummy); // Dummy
}
;

// ----------------------------------------------------------------------
sequence_def
// ----------------------------------------------------------------------
: ICE_SEQUENCE '<' meta_data type '>' ICE_IDENTIFIER
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($6);
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($3);
    TypePtr type = dynamic_pointer_cast<Type>($4);
    ContainerPtr cont = unit->currentContainer();
    $$ = cont->createSequence(ident->v, type, metaData->v);
}
| ICE_SEQUENCE '<' meta_data type '>' keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($6);
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($3);
    TypePtr type = dynamic_pointer_cast<Type>($4);
    ContainerPtr cont = unit->currentContainer();
    $$ = cont->createSequence(ident->v, type, metaData->v); // Dummy
    unit->error("keyword `" + ident->v + "' cannot be used as sequence name");
}
;

// ----------------------------------------------------------------------
dictionary_def
// ----------------------------------------------------------------------
: ICE_DICTIONARY '<' meta_data type ',' meta_data type '>' ICE_IDENTIFIER
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($9);
    StringListTokPtr keyMetaData = dynamic_pointer_cast<StringListTok>($3);
    TypePtr keyType = dynamic_pointer_cast<Type>($4);
    StringListTokPtr valueMetaData = dynamic_pointer_cast<StringListTok>($6);
    TypePtr valueType = dynamic_pointer_cast<Type>($7);
    ContainerPtr cont = unit->currentContainer();
    $$ = cont->createDictionary(ident->v, keyType, keyMetaData->v, valueType, valueMetaData->v);
}
| ICE_DICTIONARY '<' meta_data type ',' meta_data type '>' keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($9);
    StringListTokPtr keyMetaData = dynamic_pointer_cast<StringListTok>($3);
    TypePtr keyType = dynamic_pointer_cast<Type>($4);
    StringListTokPtr valueMetaData = dynamic_pointer_cast<StringListTok>($6);
    TypePtr valueType = dynamic_pointer_cast<Type>($7);
    ContainerPtr cont = unit->currentContainer();
    $$ = cont->createDictionary(ident->v, keyType, keyMetaData->v, valueType, valueMetaData->v); // Dummy
    unit->error("keyword `" + ident->v + "' cannot be used as dictionary name");
}
;

// ----------------------------------------------------------------------
enum_id
// ----------------------------------------------------------------------
: ICE_ENUM ICE_IDENTIFIER
{
    $$ = $2;
}
| ICE_ENUM keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($2);
    unit->error("keyword `" + ident->v + "' cannot be used as enumeration name");
    $$ = $2; // Dummy
}
;

// ----------------------------------------------------------------------
enum_def
// ----------------------------------------------------------------------
: enum_id
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    EnumPtr en = cont->createEnum(ident->v);
    if(en)
    {
        cont->checkIntroduced(ident->v, en);
    }
    else
    {
        en = cont->createEnum(IceUtil::generateUUID(), Dummy);
    }
    unit->pushContainer(en);
    $$ = en;
}
'{' enumerator_list '}'
{
    EnumPtr en = dynamic_pointer_cast<Enum>($2);
    if(en)
    {
        EnumeratorListTokPtr enumerators = dynamic_pointer_cast<EnumeratorListTok>($4);
        if(enumerators->v.empty())
        {
            unit->error("enum `" + en->name() + "' must have at least one enumerator");
        }
        unit->popContainer();
    }
    $$ = $2;
}
|
ICE_ENUM
{
    unit->error("missing enumeration name");
    ContainerPtr cont = unit->currentContainer();
    EnumPtr en = cont->createEnum(IceUtil::generateUUID(), Dummy);
    unit->pushContainer(en);
    $$ = en;
}
'{' enumerator_list '}'
{
    unit->popContainer();
    $$ = $1;
}
;

// ----------------------------------------------------------------------
enumerator_list
// ----------------------------------------------------------------------
: enumerator ',' enumerator_list
{
    EnumeratorListTokPtr ens = dynamic_pointer_cast<EnumeratorListTok>($1);
    ens->v.splice(ens->v.end(), dynamic_pointer_cast<EnumeratorListTok>($3)->v);
    $$ = ens;
}
| enumerator
{
}
;

// ----------------------------------------------------------------------
enumerator
// ----------------------------------------------------------------------
: ICE_IDENTIFIER
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    EnumeratorListTokPtr ens = make_shared<EnumeratorListTok>();
    ContainerPtr cont = unit->currentContainer();
    EnumeratorPtr en = cont->createEnumerator(ident->v);
    if(en)
    {
        ens->v.push_front(en);
    }
    $$ = ens;
}
| ICE_IDENTIFIER '=' enumerator_initializer
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    EnumeratorListTokPtr ens = make_shared<EnumeratorListTok>();
    ContainerPtr cont = unit->currentContainer();
    IntegerTokPtr intVal = dynamic_pointer_cast<IntegerTok>($3);
    if(intVal)
    {
        if(intVal->v < 0 || intVal->v > Int32Max)
        {
            unit->error("value for enumerator `" + ident->v + "' is out of range");
        }
        else
        {
            EnumeratorPtr en = cont->createEnumerator(ident->v, static_cast<int>(intVal->v));
            ens->v.push_front(en);
        }
    }
    $$ = ens;
}
| keyword
{
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($1);
    unit->error("keyword `" + ident->v + "' cannot be used as enumerator");
    EnumeratorListTokPtr ens = make_shared<EnumeratorListTok>(); // Dummy
    $$ = ens;
}
| %empty
{
    EnumeratorListTokPtr ens = make_shared<EnumeratorListTok>();
    $$ = ens; // Dummy
}
;

// ----------------------------------------------------------------------
enumerator_initializer
// ----------------------------------------------------------------------
: ICE_INTEGER_LITERAL
{
    $$ = $1;
}
| scoped_name
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ContainedList cl = unit->currentContainer()->lookupContained(scoped->v);
    IntegerTokPtr tok;
    if(!cl.empty())
    {
        ConstPtr constant = dynamic_pointer_cast<Const>(cl.front());
        if(constant)
        {
            unit->currentContainer()->checkIntroduced(scoped->v, constant);
            BuiltinPtr b = dynamic_pointer_cast<Builtin>(constant->type());
            if(b && b->isIntegralType())
            {
                IceUtil::Int64 v;
                if(IceUtilInternal::stringToInt64(constant->value(), v))
                {
                    tok = make_shared<IntegerTok>();
                    tok->v = v;
                    tok->literal = constant->value();
                }
            }
        }
    }

    if(!tok)
    {
        string msg = "illegal initializer: `" + scoped->v + "' is not an integer constant";
        unit->error(msg); // $$ is dummy
    }

    $$ = tok;
}
;

// ----------------------------------------------------------------------
out_qualifier
// ----------------------------------------------------------------------
: ICE_OUT
{
    BoolTokPtr out = make_shared<BoolTok>();
    out->v = true;
    $$ = out;
}
| %empty
{
    BoolTokPtr out = make_shared<BoolTok>();
    out->v = false;
    $$ = out;
}
;

// ----------------------------------------------------------------------
parameters
// ----------------------------------------------------------------------
: %empty
{
}
| out_qualifier meta_data tagged_type_id
{
    BoolTokPtr isOutParam = dynamic_pointer_cast<BoolTok>($1);
    TaggedDefTokPtr tsp = dynamic_pointer_cast<TaggedDefTok>($3);
    OperationPtr op = dynamic_pointer_cast<Operation>(unit->currentContainer());
    if(op)
    {
        ParamDeclPtr pd = op->createParamDecl(tsp->name, tsp->type, isOutParam->v, tsp->isTagged, tsp->tag);
        unit->currentContainer()->checkIntroduced(tsp->name, pd);
        StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($2);
        if(!metaData->v.empty())
        {
            pd->setMetaData(metaData->v);
        }
    }
}
| parameters ',' out_qualifier meta_data tagged_type_id
{
    BoolTokPtr isOutParam = dynamic_pointer_cast<BoolTok>($3);
    TaggedDefTokPtr tsp = dynamic_pointer_cast<TaggedDefTok>($5);
    OperationPtr op = dynamic_pointer_cast<Operation>(unit->currentContainer());
    if(op)
    {
        ParamDeclPtr pd = op->createParamDecl(tsp->name, tsp->type, isOutParam->v, tsp->isTagged, tsp->tag);
        unit->currentContainer()->checkIntroduced(tsp->name, pd);
        StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($4);
        if(!metaData->v.empty())
        {
            pd->setMetaData(metaData->v);
        }
    }
}
| out_qualifier meta_data type keyword
{
    BoolTokPtr isOutParam = dynamic_pointer_cast<BoolTok>($1);
    TypePtr type = dynamic_pointer_cast<Type>($3);
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($4);
    OperationPtr op = dynamic_pointer_cast<Operation>(unit->currentContainer());
    if(op)
    {
        op->createParamDecl(ident->v, type, isOutParam->v, false, 0); // Dummy
        unit->error("keyword `" + ident->v + "' cannot be used as parameter name");
    }
}
| parameters ',' out_qualifier meta_data type keyword
{
    BoolTokPtr isOutParam = dynamic_pointer_cast<BoolTok>($3);
    TypePtr type = dynamic_pointer_cast<Type>($5);
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($6);
    OperationPtr op = dynamic_pointer_cast<Operation>(unit->currentContainer());
    if(op)
    {
        op->createParamDecl(ident->v, type, isOutParam->v, false, 0); // Dummy
        unit->error("keyword `" + ident->v + "' cannot be used as parameter name");
    }
}
| out_qualifier meta_data type
{
    BoolTokPtr isOutParam = dynamic_pointer_cast<BoolTok>($1);
    TypePtr type = dynamic_pointer_cast<Type>($3);
    OperationPtr op = dynamic_pointer_cast<Operation>(unit->currentContainer());
    if(op)
    {
        op->createParamDecl(IceUtil::generateUUID(), type, isOutParam->v, false, 0); // Dummy
        unit->error("missing parameter name");
    }
}
| parameters ',' out_qualifier meta_data type
{
    BoolTokPtr isOutParam = dynamic_pointer_cast<BoolTok>($3);
    TypePtr type = dynamic_pointer_cast<Type>($5);
    OperationPtr op = dynamic_pointer_cast<Operation>(unit->currentContainer());
    if(op)
    {
        op->createParamDecl(IceUtil::generateUUID(), type, isOutParam->v, false, 0); // Dummy
        unit->error("missing parameter name");
    }
}
;

// ----------------------------------------------------------------------
throws
// ----------------------------------------------------------------------
: ICE_THROWS exception_list
{
    $$ = $2;
}
| %empty
{
    $$ = make_shared<ExceptionListTok>();
}
;

// ----------------------------------------------------------------------
scoped_name
// ----------------------------------------------------------------------
: ICE_IDENTIFIER
{
}
| ICE_SCOPED_IDENTIFIER
{
}
;

// ----------------------------------------------------------------------
builtin
// ----------------------------------------------------------------------
: ICE_BOOL {}
| ICE_BYTE {}
| ICE_SHORT {}
| ICE_INT {}
| ICE_LONG {}
| ICE_FLOAT {}
| ICE_DOUBLE {}
| ICE_STRING {}
| ICE_OBJECT {}
| ICE_VALUE {}

// ----------------------------------------------------------------------
type
// ----------------------------------------------------------------------
: ICE_OBJECT '*'
{
    $$ = unit->builtin(Builtin::KindObjectProxy);
}
| builtin
{
    StringTokPtr typeName = dynamic_pointer_cast<StringTok>($1);
    $$ = unit->builtin(Builtin::kindFromString(typeName->v).value());
}
| scoped_name
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    if(cont)
    {
        TypeList types = cont->lookupType(scoped->v);
        if(types.empty())
        {
            YYERROR; // Can't continue, jump to next yyerrok
        }
        TypePtr firstType = types.front();

        InterfaceDeclPtr interface = dynamic_pointer_cast<InterfaceDecl>(firstType);
        if(interface)
        {
            string msg = "add a '*' after the interface name to specify its proxy type: '";
            msg += scoped->v;
            msg += "*'";
            unit->error(msg);
            YYERROR; // Can't continue, jump to next yyerrok
        }
        cont->checkIntroduced(scoped->v);

        $$ = firstType;
    }
    else
    {
        $$ = 0;
    }
}
| scoped_name '*'
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ContainerPtr cont = unit->currentContainer();
    if(cont)
    {
        TypeList types = cont->lookupType(scoped->v);
        if(types.empty())
        {
            YYERROR; // Can't continue, jump to next yyerrok
        }
        TypePtr firstType = types.front();

        InterfaceDeclPtr interface = dynamic_pointer_cast<InterfaceDecl>(firstType);
        if(!interface)
        {
            string msg = "`";
            msg += scoped->v;
            msg += "' must be an interface";
            unit->error(msg);
            YYERROR; // Can't continue, jump to next yyerrok
        }
        cont->checkIntroduced(scoped->v);

        $$ = firstType;
    }
    else
    {
        $$ = 0;
    }
}
;

// ----------------------------------------------------------------------
string_literal
// ----------------------------------------------------------------------
: ICE_STRING_LITERAL string_literal // Adjacent string literals are concatenated
{
    StringTokPtr str1 = dynamic_pointer_cast<StringTok>($1);
    StringTokPtr str2 = dynamic_pointer_cast<StringTok>($2);
    str1->v += str2->v;
}
| ICE_STRING_LITERAL
{
}
;

// ----------------------------------------------------------------------
string_list
// ----------------------------------------------------------------------
: string_list ',' string_literal
{
    StringTokPtr str = dynamic_pointer_cast<StringTok>($3);
    StringListTokPtr stringList = dynamic_pointer_cast<StringListTok>($1);
    stringList->v.push_back(str->v);
    $$ = stringList;
}
| string_literal
{
    StringTokPtr str = dynamic_pointer_cast<StringTok>($1);
    StringListTokPtr stringList = make_shared<StringListTok>();
    stringList->v.push_back(str->v);
    $$ = stringList;
}
;

// ----------------------------------------------------------------------
const_initializer
// ----------------------------------------------------------------------
: ICE_INTEGER_LITERAL
{
    BuiltinPtr type = unit->builtin(Builtin::KindLong);
    IntegerTokPtr intVal = dynamic_pointer_cast<IntegerTok>($1);
    ostringstream sstr;
    sstr << intVal->v;
    ConstDefTokPtr def = make_shared<ConstDefTok>(type, sstr.str(), intVal->literal);
    $$ = def;
}
| ICE_FLOATING_POINT_LITERAL
{
    BuiltinPtr type = unit->builtin(Builtin::KindDouble);
    FloatingTokPtr floatVal = dynamic_pointer_cast<FloatingTok>($1);
    ostringstream sstr;
    sstr << floatVal->v;
    ConstDefTokPtr def = make_shared<ConstDefTok>(type, sstr.str(), floatVal->literal);
    $$ = def;
}
| scoped_name
{
    StringTokPtr scoped = dynamic_pointer_cast<StringTok>($1);
    ConstDefTokPtr def;
    ContainedList cl = unit->currentContainer()->lookupContained(scoped->v, false);
    if(cl.empty())
    {
        // Could be an enumerator
        def = make_shared<ConstDefTok>(SyntaxTreeBasePtr(0), scoped->v, scoped->v);
    }
    else
    {
        EnumeratorPtr enumerator = dynamic_pointer_cast<Enumerator>(cl.front());
        ConstPtr constant = dynamic_pointer_cast<Const>(cl.front());
        if(enumerator)
        {
            unit->currentContainer()->checkIntroduced(scoped->v, enumerator);
            def = make_shared<ConstDefTok>(enumerator, scoped->v, scoped->v);
        }
        else if(constant)
        {
            unit->currentContainer()->checkIntroduced(scoped->v, constant);
            def = make_shared<ConstDefTok>(constant, constant->value(), constant->value());
        }
        else
        {
            def = make_shared<ConstDefTok>();
            string msg = "illegal initializer: `" + scoped->v + "' is ";
            string kindOf = cl.front()->kindOf();
            msg += prependA(kindOf);
            unit->error(msg); // $$ is dummy
        }
    }
    $$ = def;
}
| ICE_STRING_LITERAL
{
    BuiltinPtr type = unit->builtin(Builtin::KindString);
    StringTokPtr literal = dynamic_pointer_cast<StringTok>($1);
    ConstDefTokPtr def = make_shared<ConstDefTok>(type, literal->v, literal->literal);
    $$ = def;
}
| ICE_FALSE
{
    BuiltinPtr type = unit->builtin(Builtin::KindBool);
    StringTokPtr literal = dynamic_pointer_cast<StringTok>($1);
    ConstDefTokPtr def = make_shared<ConstDefTok>(type, "false", "false");
    $$ = def;
}
| ICE_TRUE
{
    BuiltinPtr type = unit->builtin(Builtin::KindBool);
    StringTokPtr literal = dynamic_pointer_cast<StringTok>($1);
    ConstDefTokPtr def = make_shared<ConstDefTok>(type, "true", "true");
    $$ = def;
}
;

// ----------------------------------------------------------------------
const_def
// ----------------------------------------------------------------------
: ICE_CONST meta_data type ICE_IDENTIFIER '=' const_initializer
{
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($2);
    TypePtr const_type = dynamic_pointer_cast<Type>($3);
    StringTokPtr ident = dynamic_pointer_cast<StringTok>($4);
    ConstDefTokPtr value = dynamic_pointer_cast<ConstDefTok>($6);
    $$ = unit->currentContainer()->createConst(ident->v, const_type, metaData->v, value->v,
                                               value->valueAsString, value->valueAsLiteral);
}
| ICE_CONST meta_data type '=' const_initializer
{
    StringListTokPtr metaData = dynamic_pointer_cast<StringListTok>($2);
    TypePtr const_type = dynamic_pointer_cast<Type>($3);
    ConstDefTokPtr value = dynamic_pointer_cast<ConstDefTok>($5);
    unit->error("missing constant name");
    $$ = unit->currentContainer()->createConst(IceUtil::generateUUID(), const_type, metaData->v, value->v,
                                               value->valueAsString, value->valueAsLiteral, Dummy); // Dummy
}
;

// ----------------------------------------------------------------------
keyword
// ----------------------------------------------------------------------
: ICE_MODULE {}
| ICE_CLASS {}
| ICE_INTERFACE {}
| ICE_EXCEPTION {}
| ICE_STRUCT {}
| ICE_SEQUENCE {}
| ICE_DICTIONARY {}
| ICE_ENUM {}
| ICE_OUT {}
| ICE_EXTENDS {}
| ICE_THROWS {}
| ICE_VOID {}
| ICE_BOOL {}
| ICE_BYTE {}
| ICE_SHORT {}
| ICE_INT {}
| ICE_LONG {}
| ICE_FLOAT {}
| ICE_DOUBLE {}
| ICE_STRING {}
| ICE_OBJECT {}
| ICE_CONST {}
| ICE_FALSE {}
| ICE_TRUE {}
| ICE_IDEMPOTENT {}
| ICE_TAG {}
| ICE_OPTIONAL {}
| ICE_VALUE {}
;

%%
