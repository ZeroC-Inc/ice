//
// Copyright (c) ZeroC, Inc. All rights reserved.
//

jar {
    destinationDirectory = new File("${libDir}")
}

task jarSources(type:Jar, dependsOn: jar){
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
    destinationDirectory = new File("${libDir}")
}

javadoc.dependsOn(compileSlice)

javadoc {
    // We rely on Java9+ javadoc features
    source = sourceSets.main.allJava
    enabled = true
    doFirst {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    failOnError = true
    options.header = project.ext.displayName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    archiveClassifier = 'javadoc'
    destinationDirectory = new File("${libDir}")
}

project.ext.pomName = "${libDir}/${project.name}-${project.version}.pom"
apply from: "$project.ext.topSrcDir/java/gradle/maven-publish.publishing.gradle"

assemble.dependsOn(jarSources, javadocJar)

artifacts {
    archives jar, jarSources, javadocJar
}

jar {
    manifest {
        attributes("Class-Path": configurations.runtimeClasspath.resolve().collect{ "${it.name}" }.join(' '))
    }
}

if(project.name == "icebox") {
    jar {
        manifest {
            attributes("Main-Class": "com.zeroc.IceBox.Server")
        }
    }
}

clean {
    delete("${libDir}/${project.name}-${project.version}.jar")
    delete("${libDir}/${project.name}-${project.version}-sources.jar")
    delete("${libDir}/${project.name}-${project.version}-javadoc.jar")
    delete(project.ext.pomName)
}

task installJars(type: Copy, dependsOn: jarSources) {
    from "${project.ext.pomName}"
    from "${libDir}/${project.name}-${project.version}.jar"
    from "${libDir}/${project.name}-${project.version}-sources.jar"
    into "${DESTDIR}${jarDir}"
}
install.dependsOn installJars
